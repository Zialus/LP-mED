plugins {
    id 'info.solidsoft.pitest' version '1.9.11'
    id "se.patrikerdes.use-latest-versions" version "0.2.18"
    id "com.github.ben-manes.versions" version "0.46.0"
    id "net.ltgt.errorprone" version "3.0.1"
    id "com.github.spotbugs" version "5.0.13"
    id "pmd"
    id 'checkstyle'
    id "java"
    id "idea"
    id 'jacoco'
    id "org.sonarqube" version "4.0.0.2929"
    id "io.freefair.lombok" version "8.0.0-rc2"
}

sonarqube {
    properties {
        property "sonar.projectKey", "Zialus_LP-mED"
        property "sonar.organization", "zialus-github"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

group 'fcup'
version '1.0-SNAPSHOT'

Boolean enableErrorProne = System.properties['enableErrorProne']
println "[IMPORTANT INFORMATION] ErrorProne is ${enableErrorProne ? 'enabled' : 'disabled'}"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'

    implementation 'com.googlecode.lanterna:lanterna:3.2.0-alpha1'

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'
    spotbugsPlugins 'com.mebigfatguy.sb-contrib:sb-contrib:7.4.7'

    annotationProcessor "com.uber.nullaway:nullaway:0.10.10"
    errorprone 'com.google.errorprone:error_prone_core:2.18.0'
}


tasks.withType(JavaCompile).configureEach {
    options.errorprone.enabled = enableErrorProne ?: false
    options.errorprone {
        option("NullAway:AnnotatedPackages", "fcup")
    }
    options.compilerArgs += ['-Xlint:all']
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }

    reports {
        html.required = true
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'fcup.Editor'
        )
    }

    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

pmd {
    ignoreFailures = true
}

pmdMain {
    reports {
        xml.required = false
        html.required = true
    }
}

pmdTest {
    reports {
        xml.required = false
        html.required = true
    }
}

pitest {
    junit5PluginVersion = '1.1.2'
    pitestVersion = '1.11.4'
    mutators = ["ALL"]
}

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

lombok {
    version = '1.18.26'
}

spotbugs {
    toolVersion = '4.7.3'
    ignoreFailures = true
    showStackTraces = false
    showProgress = true
}

spotbugsMain {
    reports {
        xml.required = false
        html.required = true
    }
}

spotbugsTest {
    reports {
        xml.required = false
        html.required = true
    }
}

checkstyle {
    toolVersion = '10.8.0'
    showViolations = false
    ignoreFailures = true
}

checkstyleMain {
    reports {
        xml.required = false
        html.required = true
    }
}

checkstyleTest {
    reports {
        xml.required = false
        html.required = true
    }
}
