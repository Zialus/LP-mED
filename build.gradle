plugins {
    id 'info.solidsoft.pitest' version '1.4.7'
    id "se.patrikerdes.use-latest-versions" version "0.2.13"
    id "com.github.ben-manes.versions" version "0.28.0"
//    id "net.ltgt.errorprone" version "1.1.1"
    id "com.github.spotbugs" version "4.0.4"
    id "pmd"
//    id 'checkstyle'
    id "java"
    id "idea"
    id 'jacoco'
}

group 'fcup'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0'

    implementation 'com.googlecode.lanterna:lanterna:3.1.0-alpha1'

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.10.1'
    spotbugsPlugins 'com.mebigfatguy.sb-contrib:sb-contrib:7.4.7'

//    errorprone 'com.google.errorprone:error_prone_core:2.3.4'
//    errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }

    reports {
        html.enabled = true
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'fcup.Editor'
        )
    }
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

pmd {
    ignoreFailures = true
}

pitest {
    testPlugin = "junit5"
    pitestVersion = "1.5.0"
    mutators = ["ALL"]
}

pmdMain {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pmdTest {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}



jacocoTestReport {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

spotbugs {
    toolVersion = '4.0.1'
    ignoreFailures = true
}

spotbugsMain {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

spotbugsTest {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}